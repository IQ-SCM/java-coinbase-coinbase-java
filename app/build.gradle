apply from: './dependencies.gradle'
apply from: './api-keys.gradle'

buildscript {
    ext.kotlin_version = '1.2.50'
    ext.android_apt = 1.8

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.neenbedankt.gradle.plugins:android-apt:$android_apt"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

if (clientId.isEmpty() || clientSecret.isEmpty() || redirectUri.isEmpty()) {
    throw new IllegalArgumentException("Please provide the API keys and redirect URI! " +
            "https://github.com/coinbase/coinbase-android-sdk-private/tree/documentation/app#getting-started"
    )
}

def redirectScheme = redirectUri.split("://")[0]
def redirectHost = redirectUri.split("://")[1]

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.coinbase.coinbasesample"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "CLIENT_ID", "\"$clientId\""
        buildConfigField "String", "CLIENT_SECRET", "\"$clientSecret\""
        buildConfigField "String", "REDIRECT_URI", "\"$redirectUri\""

        resValue "string", "redirect_uri_scheme", "$redirectScheme"
        resValue "string", "redirect_uri_host", "$redirectHost"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':coinbase-java')

    implementation androidDependencies.androidSupport
    implementation androidDependencies.recyclerView
    implementation androidDependencies.constraintLayout
    implementation androidDependencies.customtabs

    implementation projectDependencies.picasso
    implementation (projectDependencies.rxandroid) {
        exclude group:'io.reactivex.rxjava2', module: 'rxjava'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
